plugins {
    id "org.springframework.boot" version "2.1.7.RELEASE"
    id 'java'
}

group 'info.repy'
version '1.0-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

java{
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    maven {
        url 'https://pkgs.dev.azure.com/repy/github-pipeline/_packaging/maven/maven/v1'
    }
    mavenCentral()
}

configurations {
    testAnnotationProcessor{
        extendsFrom annotationProcessor
    }
    testCompileOnly{
        extendsFrom compileOnly
    }
    serverless{
        extendsFrom implementation
        extendsFrom runtimeOnly
    }
}

dependencies {
    //spring
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation 'org.springframework.boot:spring-boot-starter-mustache'
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.yaml:snakeyaml"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // aws
    implementation platform('software.amazon.awssdk:bom:2.5.+')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:dynamodb'

    // Lombok
    def lombokVersion = "1.18.8"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'info.repy:dynamodb-converter:1.0.9'

    // serverless
    implementation("com.amazonaws.serverless:aws-serverless-java-container-spring:1.3.2"){
        exclude group:"org.slf4j", module:"slf4j-api"
    }
    implementation "com.amazonaws:aws-lambda-java-events:2.2.7"
    // serverless "io.symphonia:lambda-logging:1.0.1"

    // other
    implementation 'com.opencsv:opencsv:4.6'
    implementation 'com.ibm.icu:icu4j:65.1'
    implementation 'commons-io:commons-io:2.6'
    runtimeOnly 'mysql:mysql-connector-java:8.0.17'

}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from(configurations.serverless) {
            exclude 'tomcat-embed-*'
        }
    }
}
